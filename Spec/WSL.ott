metavar x, y, z, f ::=
indexvar i, j, k, n, m ::=

grammar
td :: top_level_decl_ ::= {{com top-level declaration}}
    | tval f <( tparam0 , .. , tparamk )> ( tval0 x0 , .. , tvalm xm ) { s0 .. sn } :: :: func_decl
    | typedef x <( tparam0 , .. , tparamk )> = tval ; :: :: typedef

tparam :: type_parameter_ ::= {{com type parameter}}
    | tval x :: :: constexpr
    | x : y0 + .. + yn :: :: type_variable

s :: stmt_ ::= {{com statement}}
    | if ( e ) s :: :: if_then {{com Desugared}}
    | while ( e ) s :: :: while {{com Desugared}}
    | for ( eOrVDecls ; eOrNothing ; eOrNothing' ) s :: :: for {{com Desugared}}
    | tval vdecl0 , .. , vdecln ; :: :: multi_vdecls {{com partly desugared}}
    | uniform tval vdecl0 , .. , vdecln ; :: :: multi_vdecls_uniform {{com partly desugared}}
    | ; :: :: empty {{com Desugared}}
    | if ( e ) s else s' :: :: if
    | do s while ( e ) ; :: :: do_while
    | switch ( e ) { sc0 : sblock0 .. scn : sblockn } :: :: switch
    | break ; :: :: break
    | continue ; :: :: continue
    | fallthrough ; :: :: fallthrough
    | return e ; :: :: return
    | return ; :: :: return_void
    | trap ; :: :: trap
    | { blockAnnot s0 .. sn } :: :: block
    | e ; :: :: effectful_expr
    | tval x : sid ; :: :: resolved_vdecl {{com post-monomorphisation variable declaration}}
    | tval x : sid = e ; :: :: resolved_init_vdecl {{com post-monomorphisation}}
    | Loop ( s , s' ) :: :: loop_construct {{com Special, only during execution}}
    | Cases ( s0 , .. , sn ) :: :: cases_construct {{com Special, only during execution}}
% TODO: check how uniform interacts with multiple declarations

sc :: switch_case_ ::=
    | case rval :: :: case
    | default :: :: default

sblock :: switch_block_ ::=
    | s0 .. sn :: :: statements

vdecl :: variable_declaration_ ::=
    | x :: :: uninitialized
    | x = e :: :: initialized

eOrVDecls :: expr_or_vdecl_ ::=
    | e :: :: expr
    | tval vdecl0 , .. , vdecln :: :: vdecl
    | :: :: nothing

eOrNothing :: expr_or_nothing_ ::=
    | e :: :: expr
    | :: :: nothing

blockAnnot :: block_annotation_ ::=
    | R :: :: exec_env {{tex _{[[R]]} }}
    | :: :: nothing

e :: expr_ ::= {{com expression}}
    | ( e ) :: :: parens
    | e , e' :: :: comma
    | e || e' :: :: or {{tex [[e]]\:{||}\:[[e']]}}
    | e && e' :: :: and {{tex [[e]]\:{\&\&}\:[[e']]}}
    | e0 ? e1 : e2 :: :: ternary {{tex [[e0]]\:{?}\:[[e1]]:[[e2]]}}
    | ! e :: :: not {{tex \:![[e]]}}
    | e == e' :: :: equals_operator
    | e != e' :: :: not_equals_operator {{com Desugared}} {{tex [[e]]\;!\mkern-\thickmuskip=[[e']]}}
    | e = e' :: :: assignment
    | x :: :: variable_name
    | * e :: :: ptr_deref
    | & e :: :: address_taking
    | @ e :: :: array_reference_making
    | e [ e' stride ] :: :: array_index {{tex [[e]] [ [[e']] ]_{[[stride]]} }}
    | x <( targ0 , .. , targm )> ( e0 , .. , en ) :: :: call
    | fid <( rv0 , .. , rvn )> ( e0 , .. , em ) :: :: resolved_call {{com post-monomorphisation, calls are resolved, and pure type arguments are gone}}
    | val :: :: val {{com only during exec, except literals}}
    | Call s :: :: call_construct {{com only during exec}}

val :: val_ ::=
    | rval :: :: rval
    | LVal ( addr ) :: :: lval
    | Invalid_LVal :: :: invalid_lval {{com corresponds to null and out-of-bounds accesses}}

stride :: stride_ ::=
    | k :: :: stride {{com stride annotation added during monomorphisation}}
    | :: :: nothing {{com no stride annotation}}

addr :: addr_ ::=
    | addr + i * k :: :: add_multiple_stride
    | sid :: :: sid

targ :: type_argument_ ::= {{com type argument}}
    | x :: :: ident {{com either a type or a constexpr}}
    | tval :: :: type {{com a type that is not just an identifier}}
    | x . y :: :: enum_value {{com a field of an enum, for a constexpr type parameter}}

G {{tex \Gamma}} , Gglobal {{tex \Gamma_{global} }} :: env_ ::= {{com typing environment}}
    | G [ x -> envMapping ] :: :: update {{com $\Gamma$ with the mapping for x replaced by envMapping}}
    | { x0 -> envMapping0 , .. , xn -> envMappingn } :: :: set

envMapping :: env_mapping_ ::= 
    | t :: :: var {{com $x$ is of type $\tau$}}
    | Func { sig0 , .. , sign } :: :: func {{com $x$ is a function whose signatures are $sig0$ to $sign$}}
    | SyntaxFunc { sigx0 , .. , sigxn } :: :: func_syntax
    | Nothing :: :: nothing {{tex \emptyset}} {{com to remove $x$ from $\Gamma$}}
    | Protocol namedSigs :: :: protocol
    % TODO: I now believe that this is wrong, and that protocols should live in their own namespace.
    % Otherwise it is impossible to refer to a protocol as both a type variable (aka Self) and as a constraint on other type variables, when defining its signatures

namedSigs :: named_signatures_ ::=
    | { x0 -> sig0 , .. , xn -> sign } :: :: explicit
    | U namedSigs0 .. namedSigsn :: :: union

sigx :: signature_syntax_ ::=
    | <( tparamAnon0 , .. , tparamAnonm )> ( tval0 , .. , tvaln ) -> tval :: :: sigx

sig :: signature_ ::=
    | <( tparamAnon0 , .. , tparamAnonm )> ( tval0 , .. , tvaln ) -> tval :: :: sig
    % TODO: fix these tparamAnanon, they are clearly wrong.

tparamAnon :: tparam_anon_ ::=
    | t :: :: constexpr
    | { y0 , .. , yn } :: :: tvar 

u :: uniformity_ ::=
    | + :: :: uniform
    | - :: :: nonuniform
    | u1 /\ u2 :: :: join {{tex [[u1]] \cap [[u2]]}}

B :: behaviour_ ::= {{com statement behaviour}}
    | { b0 , .. , bn } :: :: set
    | B + B' :: :: union {{tex [[B]] \cup [[B']]}}
    | B \ B' :: :: difference {{tex [[B]] \backslash [[B']]}}
    | U B0 .. Bn :: :: big_union
    | B | u :: :: restrict
    | ( B ) :: :: parens

b :: single_behaviour_ ::=
    | Return u tval :: :: return
    | Break u :: :: break
    | Continue u :: :: continue
    | Fallthrough u :: :: fallthrough
    | Nothing :: :: Nothing

t {{tex \tau}} :: type_ ::= {{com type}}
    | LVal ( tval , u , addressSpace ) :: :: lval {{com left value}}
    | tval u :: :: tval {{tex [[tval]]_{[[u]]} }}
tval {{tex {\tau^{val} } }} :: type_value_ ::=
    | Ptr ( tval , addressSpace ) :: :: ptr {{com pointer}}
    | Ref ( tval , addressSpace ) :: :: array_ref {{com array reference}}
    | [ tval ] :: :: array {{com array}}
    | bool :: :: bool
    | uint32 :: :: uint32 {{tex \textbf{uint32} }}
    | void :: :: void
    | TVar tid namedSigs :: :: tvar
addressSpace, as :: address_space_ ::=
    | thread :: :: thread
    | threadgroup :: :: threadgroup
    | device :: :: device
    | constant :: :: constant
tid :: type_identifier_ ::=

rval, rv :: rval_ ::=
    | { x0 : rval0 ; .. ; xn : rvaln } :: :: struct
    | [ rval0 , .. , rvaln ] :: :: array
    | Ptr ( addr ) :: :: ptr 
    | Ref ( addr , j ) :: :: ref {{com Reference to array of size j}}
    | null :: :: lit_null
    | true :: :: lit_true
    | false :: :: lit_false
    | uint :: :: lit_uint {{com unsigned integer literal}}
    | TrapValue :: :: trap {{com Special, represents a caught error (e.g. out-of-bounds access)}}
    | Void :: :: void {{com Special, the return value of a void function call}}

R {{tex \rho}}, Rout {{tex \rho_{out} }} :: exec_env_ ::= {{com execution environment}}
    | R [ x0 -> val0 , .. , xn -> valn ] :: :: update_with_vals
    | Empty :: :: empty {{tex \emptyset}}

E :: memory_event_ ::= {{com memory event}}
    | :: :: nothing
    | addr <- rval :: :: store {{com store}}
    | addr -> rval :: :: load {{com load}}
    | Sequence ( E0 , .. , En ) :: :: multiple_events

fid :: function_identifier_ ::=

terminals :: terminals_ ::=
    | U :: :: big_union {{tex \bigcup}}
    | |- :: :: vdash {{tex \vdash}}
    | <- :: :: load {{tex \leftarrow}}
    | -> :: :: mapsto {{tex \mapsto}}
    | --> :: :: desugars {{tex \leadsto}}
    | <( :: :: generic_open {{tex {<} }} % For removing extraneous spaces around '<' and '>' when they are used in that position.
    | )> :: :: generic_close {{tex {>} }}
    | in :: :: in {{tex \in}}
    | \/ :: :: math_or {{tex \vee}}
    | /\ :: :: math_and {{tex \wedge}}
    | <= :: :: math_lesser_equal {{tex \leq}}
    | >= :: :: math_greater_equal {{tex \geq}}

formula :: formula_ ::=
    | judgement :: :: judgement
    | formula0 /\ .. /\ formulan :: :: several_formula
    | formula \/ formula' :: :: or
    | n > 0 :: :: int_positive
    | x -> envMapping in G :: :: env_mapping_exists
    | x not in G :: :: env_mapping_missing {{tex [[x]] \not\in [[G]]}}
    | G |- isIntegerOrEnum ( tval ) :: :: is_integer
    | G |- sc0 .. scn fully covers tval :: :: full_switch_coverage % TODO: make it explicit
    | s != s' :: :: stmt_not_eq {{tex [[s]] \neq [[s']]}}
    | tval != tval' :: :: tval_not_eq {{tex [[tval]] \neq [[tval']]}}
    | as != as' :: :: address_space_not_eq {{tex [[as]] \neq [[as']]}}
    | b in B :: :: behaviour_in {{tex [[b]] \in [[B]]}}
    | b not in B :: :: behaviour_not_in {{tex [[b]] \not\in [[B]]}}
    | G = G' :: :: typing_env_eq
    | B = B' :: :: behaviour_eq
    | namedSigs = namedSigs' :: :: named_signature_eq
    | sig = sig' :: :: sig_eq
    | e = e' :: :: expr_eq
    | e != e' :: :: expr_neq {{tex [[e]] \neq [[e']]}}
    | exists i . formula :: :: exists_int
    | tid = makeFresh()  :: :: make_fresh_tid
    | i = uint :: :: uint_to_indexvar
    | i <= n :: :: indexvar_leq
    | i >= n :: :: indexvar_geq
    | i < n :: :: indexvar_lesser
    | x -> val in R :: :: val_in_env
    | fid -> <( x0 , .. , xn )> ( y0 : addr0 , .. , ym : addrm ) { s0 .. sk } :: :: fid_resolving
    | E = E' :: :: event_eq
    | R = R' :: :: exec_env_eq
    | s = s' :: :: stmt_eq
    | rv not in sc0 .. scn :: :: rval_not_in_cases % TODO: fix typesetting
    | s = { sblock } :: :: block_from_switch_block
    | rv = Default ( tval ) :: :: default_value
    | s is a terminator :: :: stmt_is_terminator
    | u <: u' :: :: uniform_subtype 
    | rval a valid value for this type :: :: valid_value
    | rval not a valid value for this type :: :: invalid_value


defns
desugaring :: '' ::=
defn
s --> s' :: :: desugaring_stmt :: '' {{com Desugaring statements}} by

    ----------------------------- :: if_then
    if (e) s --> if (e) s else {}

    -------- :: empty_stmt
    ; --> {}

    -------------------------------------- :: while
    while (e) s --> if (e) do s while (e);

    -------------------------------------------------------------------------- :: for_empty_cond
    for (eOrVDecls ; ; eOrNothing) s --> for (eOrVDecls ; true ; eOrNothing) s

    --------------------------------------------------------------------------- :: for_empty_incr
    for (eOrVDecls ; e ; ) s --> for (eOrVDecls ; e ; null) s

    ------------------------------------------------ :: for_init_expr
    for (e ; e' ; e'') s --> {e; while(e') {s e'';}}

    ------------------------------------------------ :: for_init_empty
    for ( ; e' ; e'') s --> while(e') {s e'';}

    ------------------------------------------------------------------------------------------ :: for_init_vdecls
    for (tval vdecl0 , .. , vdecln ; e' ; e'') s --> {tval vdecl0 , .. , vdecln; while(e') {s e'';}}

    k > 0
    -------------------------------------------------------------------------------------- :: multiple_vdecls
    { s0..sn tval vdecl0, vdecl1, .., vdeclk; s'0..s'm} --> {s0..sn tval vdecl0; tval vdecl1, .., vdeclk; s'0..s'm}

    ------------------------------------------------------------- :: initialized_vdecl
    { s0..sn tval x = e; s'0..s'm} --> {s0..sn tval x; x = e; s'0..s'm}

% TODO: replace foo(e0,..,en) by foo<>(e0,..,en)
% Both in expressions, and in top-level declarations
% TODO: also desugar syntactic types that have an addressSpace as a prefix.
% Also make it an error to have an addressSpace and neither array ref nor ptr.
defn
e --> e' :: :: desugaring_expr :: '' {{com Desugaring expressions}} by
    
    ----------------------- :: not_equals_operator
    e != e' --> ! (e == e')

%   defns
%   well_formed :: '' ::=
%   defn
%   G |- well_formed ( td ) :: :: top_level :: '' by
%
%       G |- {s0..sn} : {Return tval}
%       ---------------------------------- :: func_trivial
%       G |- well_formed(tval f<()>() {s0..sn})
%
%       G[x0 -> LVal(tval0, thread)] |- well_formed (tval f<()>(tval1 x1, .., tvalm xm) {s0..sn})
%       -------------------------------------------------------------------- :: func_param
%       G |- well_formed (tval f<()>(tval0 x0, tval1 x1, .., tvalm xm) {s0..sn})
%
%       G[x -> tval'] |- well_formed (tval f<(tparam0, .., tparamk)> (tval0 x0, .., tvalm xm) {s0..sn}) 
%       ---------------------------------------------------------------------------------------- :: func_constexpr
%       G |- well_formed (tval f<(tval' x, tparam0, .., tparamk)> (tval0 x0, .., tvalm xm) {s0..sn}) 
%
%       y0 -> Protocol namedSigs0 in G /\ .. /\ yi -> Protocol namedSigsi in G
%       namedSigs = U namedSigs0 .. namedSigsi
%       tid = makeFresh()
%       G[x -> TVar tid namedSigs] |- well_formed (tval f<(tparam0, .., tparamk)> (tval0 x0, .., tvalm xm) {s0..sn})
%       ------------------------------------------------------------------------------------------------------ :: func_tvar
%       G |- well_formed (tval f<(x : y0 + .. + yi, tparam0, .., tparamk)> (tval0 x0, .., tvalm xm) {s0..sn}) 
%       % TODO: maybe add some check here that the different protocols are not incompatible.
%       % Can we even have incompatible protocols?

defns
typing :: '' ::=
defn
G |- s : u B :: :: typing_statement :: '' {{com Validating statements' behaviours}} {{tex [[G]] \vdash [[s]] :_{[[u]]} [[B]]}} by

    G |- e : u2 bool u1
    G |- s : u1 /\ u2 B
    G |- s' : u1 /\ u2 B'
    B'' = (B + B') | u1 /\ u2
    Return u Ptr(tval, as) not in B''
    Return u Ref(tval, as) not in B''
    ------------------------------ :: if
    G |- if (e) s else s' : u2 B''

    G |- e : + bool +
    G |- s : + B
    Return u Ptr(tval, as) not in B
    Return u Ref(tval, as) not in B
    Return - tval not in B
    Break - not in B
    Continue - not in B
    Fallthrough - not in B
    ------------------------------------------------------------ :: do_while_uniform
    G |- do s while (e); : + (B \ {Break +, Continue +}) + {Nothing}

    G |- e : - bool -
    G |- s : - B
    Return u Ptr(tval, as) not in B
    Return u Ref(tval, as) not in B
    ---------------------------------------------------------- :: do_while
    G |- do s while (e); : u1 (B \ {Break +, Break -, Continue +, Continue -}) + {Nothing}

    G |- e : + tval +
    G |- isIntegerOrEnum(tval)
    G |- sc0: tval /\ .. /\ G |- scn: tval
    G |- sc0 .. scn fully covers tval
    G |-- sblock0: + B0 /\ .. /\ G |-- sblockn: + Bn
    B = U B0 .. Bn
    Nothing not in B
    Return u Ptr(tval, as) not in B
    Return u Ref(tval, as) not in B
    Return - tval not in B
    Break - not in B
    Continue - not in B
    Fallthrough - not in B
    -------------------------------------------------------------------------------------------- :: switch_uniform
    G |- switch (e) {sc0: sblock0 .. scn : sblockn } : + (B \ {Break +, Fallthrough +}) + {Nothing}

    G |- e : u1 tval -
    G |- isIntegerOrEnum(tval)
    G |- sc0: tval /\ .. /\ G |- scn: tval
    G |- sc0 .. scn fully covers tval
    G |-- sblock0: - B0 /\ .. /\ G |-- sblockn: - Bn
    B = U B0 .. Bn
    Nothing not in B
    Return u Ptr(tval, as) not in B
    Return u Ref(tval, as) not in B
    -------------------------------------------------------------------------------------------- :: switch
    G |- switch (e) {sc0: sblock0 .. scn : sblockn } : u1 (B \ {Break +, Break -, Fallthrough +, Fallthrough -}) + {Nothing}

    --------------------- :: break
    G |- break; : u {Break u}

    --------------------------- :: continue
    G |- continue; : u {Continue u}

    ------------------------------------- :: fallthrough
    G |- fallthrough; : u {Fallthrough u}

    G |- e : u2 tval u1
    ---------------------------------------- :: return
    G |- return e; : u2 {Return u2 tval}

    ----------------------------- :: return_void
    G |- return ; : u {Return u void}

    -------------------------- :: trap
    G |- trap; : u {Return u tval}

    ------------------- :: empty_block
    G |- {} : u {Nothing}

    tval != Ptr(tval', as)
    tval != Ref(tval', as)
    G[x -> LVal(tval, u2, thread)] |- {s0 .. sn} : u1 B
    u2 <: u1
    s0 != tval' x; /\../\ sn != tval' x;
    s0 != tval' x = e'; /\../\ sn != tval' x = e';
    -------------------------------------------- :: variable_decl
    G |- {tval x; s0 .. sn} : u1 B
    % Note: the last two premises prevent redeclaration of a variable in the same scope it was declared in.
    % Implemented naively it takes O(n**2) to check, but can easily be optimized.

    tval != Ptr(tval', as)
    tval != Ref(tval', as)
    G[x -> LVal(tval, +, thread)] |- {s0 .. sn} : + B
    s0 != tval' x; /\../\ sn != tval' x;
    s0 != tval' x = e'; /\../\ sn != tval' x = e';
    -------------------------------------------- :: variable_decl_uniform
    G |- {uniform tval x; s0 .. sn} : + B

    G[x -> LVal(tval, u3, thread)] |- e : u1 tval u2
    G[x -> LVal(tval, u3, thread)] |- {s0 .. sn} : u1 B
    u3 <: u1 /\ u2
    s0 != tval' x; /\../\ sn != tval' x;
    s0 != tval' x = e'; /\../\ sn != tval' x = e';
    -------------------------------------------- :: variable_decl_init
    G |- {tval x = e; s0 .. sn} : u1 B

    G[x -> LVal(tval, +, thread)] |- e : + tval +
    G[x -> LVal(tval, +, thread)] |- {s0 .. sn} : + B
    s0 != tval' x; /\../\ sn != tval' x;
    s0 != tval' x = e'; /\../\ sn != tval' x = e';
    -------------------------------------------- :: variable_decl_init_uniform
    G |- {uniform tval x = e; s0 .. sn} : + B

    G |- s : u B
    ------------ :: trivial_block
    G |- {s} : u B

    G |- s : + B
    G |- {s1 .. sn} : + B'
    n > 0
    Return - tval not in B
    Break - not in B
    Continue - not in B
    Fallthrough u not in B
    Nothing in B
    -------------------------------------- :: block_uniform
    G |- {s s1 .. sn} : + (B \ {Nothing}) + B'
    % Note: the last premise forbids trivially dead code. It is optional and could be removed with no consequences on the rest of the language.
    % Note: the penultimate premise makes it a bit easier to compile to MSL, could be relaxed in future versions of the language.

    G |- s : u B
    G |- {s1 .. sn} : - B'
    n > 0
    Fallthrough u not in B
    Nothing in B
    -------------------------------------- :: block
    G |- {s s1 .. sn} : u (B \ {Nothing}) + B'

    G |- e : u2 t
    ---------------------- :: expr
    G |- e; : u2 {Nothing}

    G |- s : - B
    ------------ :: uniformity
    G |- s : + B
    % Anything that is valid in non-uniform control-flow is also valid in uniform control-flow
    % TODO: find out if the previous rules can be simplified from that.

defn
G |- sc : tval :: :: typing_switch_case :: '' by

    G |- rval : + tval +
    --------------------- :: case
    G |- case rval : tval
    % Note: I put rval :+ tval+ in the premise instead of tval-, but it does not matter as only constexprs are allowed in that position in the grammar.

    ------------------- :: default
    G |- default : tval

defn
G |-- sblock : u B :: :: typing_switch_block :: '' {{tex [[G]] \vdash [[sblock]] :_{[[u]]} [[B]]}} by

    G |- { s0 .. sn } : u B
    --------------------- :: switch_block
    G |-- s0 .. sn : u B

defn
G |- e : u t :: :: typing_expr :: '' {{com Typing expressions}} {{tex [[G]] \vdash [[e]] :_{[[u]]} [[t]]}} by

    ---------------------------- :: null_lit_array_ref
    G |- null : u Ref (tval, as) +

    ---------------------------- :: null_lit_ptr
    G |- null : u Ptr (tval, as) +

    ------------------ :: literal_true
    G |- true : u bool +

    ------------------- :: literal_false
    G |- false : u bool +

    G |- e : u t
    ------------ :: parens
    G |- (e) : u t

    G |- e : u t
    G |- e' : u t'
    ----------------- :: comma
    G |- e, e' : u t'

    G |- e : u bool u1
    G |- e' : u/\u1 bool u2
    ------------------- :: or
    G |- e || e' : u bool u1/\u2

    G |- e : u bool u1
    G |- e' : u/\u1 bool u2
    ---------------------------- :: and
    G |- e && e' : u bool u1/\u2

    G |- e0 : u bool u0
    G |- e1 : u/\u0 tval u1
    G |- e2 : u/\u0 tval u2
    tval != Ptr(tval', as)
    tval != Ref(tval', as)
    --------------------------------- :: ternary
    G |- e0 ? e1 : e2 : u tval u1/\u2

    G |- e : u bool u1
    ------------------- :: not
    G |- !e : u bool u1

    G |- e : + LVal(tval, +, as)
    G |- e' : + tval +
    as != constant
    tval != Ptr(tval', as')
    tval != Ref(tval', as')
    ----------------------- :: assignment_uniform
    G |- e = e' : + tval +
    % If the lval is uniform, then so must everything else (the control-flow and the right-side value, and the control-flow is the same in the premises).

    G |- e : u LVal(tval, -, as)
    G |- e' : u tval u1
    as != constant
    tval != Ptr(tval', as')
    tval != Ref(tval', as')
    ------------------------ :: assignment
    G |- e = e' : u tval u1

    x -> t in G
    ------------ :: variable_name
    G |- x : u t

    G |- e : u LVal(tval, u1, as)
    ----------------------------- :: lval_access
    G |- e : u tval u1

    G |- e : u LVal(tval, -, as)
    ----------------------------- :: address_taking
    G |- &e : u Ptr(tval, as) -
    % can the unary operator & be overloaded?
    % It seems that no
    % Note: We currently forbid taking the address of a uniform variable, as aliasing makes the inference problem nightmarish

    G |- e : u Ptr(tval, as) -
    ------------------------------ :: ptr_deref
    G |- *e : u LVal(tval, -, as)
    % can the unary operator * be overloaded?
    % It seems that no
    % Note: We currently forbid any kind of uniform access to anything that might be aliased

    % Note: We currently do not have any special interaction between pointers and array references in these rules
    
    G |- e : u LVal(tval, -, as)
    ---------------------------- :: take_ref_lval
    G |- @e : u Ref(tval, as) -
    % Note: in the execution rules, the behaviour depends on whether that LVal points to an array, but here we don't need to track it.

    G |- e : u LVal([tval], +, as)
    G |- e' : u uint32 +
    --------------------------- :: array_index_lval_uniform
    G |- e[e'] : u LVal(tval, +, as)

    G |- e : u LVal([tval], -, as)
    G |- e' : u uint32 -
    --------------------------- :: array_index_lval_non_uniform
    G |- e[e'] : u LVal(tval, -, as)

    G |- e : u [tval] u1
    G |- e' : u uint32 u2
    -------------------------- :: array_index_rval
    G |- e[e'] : u tval u1/\u2 
    % There is a choice between applying array_index_lval and then lval_access, or lval_access and then array_index_rval.
    % It is not problematic, because the rules are confluent, so either choice leads to the same result.
    % TODO: should we refuse during validation the case where e' is a constant that falls out of the bounds of e ?
    % I think it should be an allowed behaviour but not required of the implementation.

    G |- e : u Ref(tval, as) -
    G |- e' : u uint32 -
    --------------------------- :: array_ref_index
    G |- e[e'] : u LVal(tval, -, as)

    % G |- e0 : tval0 /\../\ G |- en : tvaln
    % x -> Func{sig0, .., sigk} in G
    % exists i . sigi = <( )> (tval0, .., tvaln) -> tval
    % -------------------------------------------------- :: call_no_targ
    % G |- x <( )> (e0, .., en) : tval
    % TODO: Make a formal rule for calls, that takes into account the way we deal with uniform (aka inlining everything)

    G |- e : u tval +
    ----------------- :: tval_uniform
    G |- e : u tval -
    % A uniform right-value type is also a non-uniform right-value type.
    % Very importantly, this does not apply to left-value types.

defns
exec :: '' ::=
defn
R |- e -> e' ; E :: :: exec_expr :: '' {{com Small-step reduction on expressions}} {{tex [[R]] \vdash [[e]] \xrightarrow{[[E]]} [[e']]}} by

    ------------------------------------------- :: and_true
    R |- true && e -> e ;

    --------------------------- :: and_false
    R |- false && e -> false ;

    R |- e0 -> e0' ; E
    ---------------------------------- :: and_reduce
    R |- e0 && e1 -> e0' && e1 ; E

    ------------------------- :: or_true
    R |- true || e -> true ;

    ----------------------- :: or_false
    R |- false || e -> e ;

    R |- e0 -> e0' ; E
    ------------------------------ :: or_reduce
    R |- e0 || e1 -> e0' || e1 ; E

    --------------------------- :: ternary_true
    R |- true ? e1 : e2 -> e1 ;

    ---------------------------- :: ternary_false
    R |- false ? e1 : e2 -> e2 ;

    R |- e0 -> e0' ; E
    ------------------------------------------ :: ternary_reduce
    R |- e0 ? e1 : e2 -> e0' ? e1 : e2 ; E

    ---------------------- :: comma_next
    R |- rval, e1 -> e1 ;

    R |- e0 -> e0' ; E
    --------------------------- :: comma_reduce
    R |- e0, e1 -> e0', e1 ; E

    ------------------ :: parens_exec
    R |- ( e ) -> e ;

    ----------------------- :: not_true
    R |- ! true -> false ;

    ----------------------- :: not_false
    R |- ! false -> true ;

    -------------------------------- :: deref_ptr
    R |- * Ptr(addr) -> LVal(addr) ;

    ----------------------------- :: deref_null
    R |- * null -> Invalid_LVal ;

    R |- e -> e' ; E
    -------------------- :: deref_reduce
    R |- * e -> * e' ; E

    e != LVal(addr)
    R |- e -> e' ; E
    --------------------- :: take_ptr_reduce
    R |- & e -> & e' ; E

    -------------------------------- :: take_ptr_lval
    R |- & LVal(addr) -> Ptr(addr);

    ---------------------------- :: take_ptr_invalid
    R |- & Invalid_LVal -> null;

    e != LVal(addr)
    R |- e -> e' ; E
    -------------------- :: make_ref_reduce
    R |- @ e -> @ e' ; E

    ---------------------------------- :: make_ref_lval
    R |- @ LVal(addr) -> Ref(addr, i);
    % TODO: find a way to express the fact that i is decided at compile time.. it should probably be made part of the @ expression

    ---------------------------- :: make_ref_invalid
    R |- @ Invalid_LVal -> null;

    % Note: we might want to eliminate the next few rules, and instead put them into the definition of operator[] on the default types?
    % it would allow for nicer interaction with protocols.
    e0 != LVal(addr)
    R |- e0 -> e0' ; E
    ---------------------------- :: array_left_reduce
    R |- e0 [e1] -> e0' [e1] ; E

    e0 = LVal(addr) \/ e0 = [rval0, .., rvaln] \/ e0 = Ref(addr, j)
    R |- e1 -> e1' ; E
    --------------------------------------------------------------- :: array_right_reduce
    R |- e0 [e1] -> e0 [e1'] ; E
    
    i = uint
    i <= n
    ------------------------------------------ :: array_lit_access_valid
    R |- [rval0, .., rvaln] [uint] -> rvali ;

    i = uint
    i > n
    --------------------------------------------- :: array_lit_access_trap
    R |- [rval0, .., rvaln] [uint] -> TrapValue ;

    i = uint
    i > n
    j <= n
    ----------------------------------------- :: array_lit_access_clamped
    R |- [rval0, .., rvaln] [uint] -> rvalj ;

    i = uint
    i <= n
    ----------------------------------------------- :: array_lval_access_valid
    R |- LVal(addr) [uint k] -> LVal(addr + i*k) ;
    % TODO: express better the size check, making it clear that the size information comes from the typing phase.

    i = uint
    i > n
    ------------------------------------------ :: array_lval_access_invalid
    R |- LVal(addr) [uint k] -> Invalid_LVal ;

    i = uint
    i > n
    j <= n
    ---------------------------------------------- :: array_lval_access_clamped
    R |- LVal(addr) [uint k] -> LVal(addr + j*k) ;

    i = uint
    i < j
    ------------------------------------------------- :: array_ref_access_valid
    R |- Ref(addr, j) [uint k] -> LVal(addr + i*k) ;

    i = uint
    i >= j
    ------------------------------------------ :: array_ref_access_invalid
    R |- Ref(addr, j) [uint k] -> Invalid_LVal ;

    i = uint
    i >= j
    n < j
    ------------------------------------------------ :: array_ref_access_clamped
    R |- Ref(addr, j) [uint k] -> LVal(addr + n*k) ;

    ------------------------------------ :: array_null_access
    R |- null [uint k] -> Invalid_LVal ;

    x -> val in R
    ---------------- :: environment_access
    R |- x -> val ;

    rv = Default(tval)
    ----------------------- :: load_invalid_default
    R |- Invalid_LVal -> rv

    ------------------------------- :: load_invalid_trap
    R |- Invalid_LVal -> TrapValue;

    rval a valid value for this type
    --------------------------------------- :: load_valid
    R |- LVal(addr) -> rval ; addr -> rval

    rval1 not a valid value for this type
    rval2 a valid value for this type
    ------------------------------------------- :: load_invalid
    R |- LVal(addr) -> rval2 ; addr -> rval1

    e0 != LVal(addr)
    e0 != Invalid_LVal
    R |- e0 -> e0' ; E
    ----------------------------- :: assign_left_reduce
    R |- e0 = e1 -> e0' = e1 ; E

    R |- e1 -> e1' ; E
    ---------------------------- :: assign_right_reduce
    R |- e0 = e1 -> e0 = e1' ; E

    --------------------------------------------- :: assign_execute
    R |- LVal(addr) = rval -> rval ; addr <- rval

    ---------------------------------- :: assign_invalid_ignore
    R |- Invalid_LVal = rval -> rval ;

    --------------------------------------- :: assign_invalid_trap
    R |- Invalid_LVal = rval -> TrapValue ;

    R |- e -> e' ; E
    ------------------------------------------------------------------------------------------------------------------ :: call_reduce
    R |- fid<(rv0, .., rvn)>(rv'0, .., rv'm, e, e0, .., ek) -> fid<(rv0, .., rvn)>(rv'0, .., rv'm, e', e0, .., ek) ; E

    fid -> <(x0, .., xn)>(y0:addr0, .., ym:addrm) {s0 .. sk}
    E = Sequence(addr0 <- rv'0, .., addrm <- rv'm)
    R' = R[x0 -> rv0, .., xn -> rvn]
    R'' = R'[y0 -> LVal(addr0), .., ym -> LVal(addrm)]
    -------------------------------------------------------------------- :: call_resolve
    R |- fid<(rv0, .., rvn)>(rv'0, .., rv'm) -> Call {R'' s0 .. sk} ; E

    R |- s -> s' ; E
    --------------------------- :: call_construct_reduce
    R |- Call s -> Call s' ; E

    -------------------------------- :: call_return
    R |- Call return rval; -> rval ;

    --------------------------- :: call_return_void
    R |- Call return; -> Void ;

    ------------------------ :: call_end_function
    R |- Call {R'} -> Void ;

defn
R |- s -> s' ; E :: :: exec_stmt :: '' {{com Small-step reduction on statements}} {{tex [[R]] \vdash [[s]] \xrightarrow{[[E]]} [[s']]}} by

    ----------------------------- :: block_annotate
    R |- {s0..sn} -> {R s0..sn};

    R |- s -> s' ; E
    ----------------------------------------- :: block_reduce
    Rout |- {R s s1..sn} -> {R s' s1..sn} ; E

    --------------------------------------- :: block_next_stmt
    Rout |- {R {R'} s1..sn} -> {R s1..sn};

    s = break; \/ s = continue; \/ s = fallthrough; \/ s = return rval; \/ s = return; \/ s = trap;
    ----------------------------------------------------------------------------------------------- :: block_terminator
    Rout |- {R s s1..sn} -> s;

    R' = R[x -> LVal(sid)]
    rv = Default(tval)
    ----------------------------------------------------------- :: block_vdecl
    Rout |- {R tval x : sid; s1..sn} -> {R' s1..sn} ; sid <- rv

    R[x -> LVal(sid)] |- e -> e';
    ----------------------------------------------------------------------- :: block_vdecl_reduce
    Rout |- {R tval x : sid = e; s1..sn} -> {R tval x : sid = e'; s1..sn} ;

    R' = R[x -> LVal(sid)]
    ---------------------------------------------------------------- :: block_vdecl_complete
    Rout |- {R tval x : sid = rv; s1..sn} -> {R' s1..sn} ; sid <- rv

    R |- e -> e' ; E
    ------------------ :: effectful_expr_reduce
    R |- e; -> e'; ; E

    ------------------ :: effectful_expr_elim
    R |- rval; -> {} ;

    R |- e -> e' ; E
    -------------------------------- :: return_reduce
    R |- return e; -> return e'; ; E

    R |- e -> e' ; E
    ---------------------------------------------- :: if_reduce
    R |- if (e) s else s' -> if (e') s else s' ; E

    ----------------------------- :: if_true
    R |- if (true) s else s' -> s;
    
    -------------------------------- :: if_false
    R |- if (false) s else s' -> s';

    -------------------------------------------------------------- :: do_while_loop
    R |- do s while(e); -> Loop(s, if(e) do s while(e); else {});

    R |- s1 -> s1' ; E
    -------------------------------------- :: loop_reduce
    R |- Loop(s1, s2) -> Loop(s1', s2) ; E

    ----------------------------- :: loop_break
    R |- Loop(break;, s2) -> {};

    s1 = {R'} \/ s1 = continue;
    --------------------------- :: loop_next_iteration
    R |- Loop(s1, s2) -> s2;
    
    s1 = fallthrough; \/ s1 = return; \/ s1 = return rval; \/ s1 = trap;
    -------------------------------------------------------------------- :: loop_other_terminator
    R |- Loop(s1, s2) -> s1;

    R |- e -> e' ; E
    ---------------------------------------------------------------------------------------------- :: switch_reduce
    R |- switch(e) {sc0:sblock0 .. scn:sblockn} -> switch(e') {sc0:sblock0 .. scn:sblockn} ; E

%TODO: the next two rules don't fit on the page. I should find a way to compact them. Maybe with the </xi//i/> notation? 
    s = {sblock} /\ s0 = {sblock'0} /\ .. /\ sm = {sblock'm}
    --------------------------------------------------------------------------------------------------------------------- :: switch_case_found
    R |- switch(rv) {sc0:sblock0 .. scn:sblockn case rv: sblock sc'0:sblock'0 .. sc'm:sblock'm} -> Cases(s, s0, .., sm) ;

    rv not in sc0 .. scn
    rv not in sc'0 .. sc'm
    s = {sblock} /\ s0 = {sblock'0} /\ .. /\ sm = {sblock'm}
    --------------------------------------------------------------------------------------------------------------------- :: switch_default
    R |- switch(rv) {sc0:sblock0 .. scn:sblockn default: sblock sc'0:sblock'0 .. sc'm:sblock'm} -> Cases(s, s0, .., sm) ;

    R |- s -> s' ; E
    ------------------------------------------------------ :: cases_reduce
    R |- Cases(s, s0, .., sn) -> Cases(s', s0, .., sn) ; E

    ---------------------------------------------------------- :: cases_fallthrough
    R |- Cases(fallthrough;, s0, .., sn) -> Cases(s0, .., sn);

    -------------------------------------- :: cases_break
    R |- Cases(break;, s0, .., sn) -> {} ;

    s = continue; \/ s = return; \/ s = return rval; \/ s = trap;
    ------------------------------------------------------------- :: cases_other_terminator
    R |- Cases(s, s0, .., sn) -> s ;
